#!/usr/bin/env perl

use strict;
use warnings;

use Docopt;
use Filesys::Notify::Simple;

my $opts = docopt();

my (@dirs) = @{$opts->{'<dirs>'}};
push @dirs, '.' unless @dirs;

my $watcher = build_watcher(@dirs);
while (1) {
    my @events;
    $watcher->wait(
        sub {
            foreach my $event (@_) {
                next unless is_valid($event->{path});

                next if grep { $_->{path} eq $event->{path} } @events;
                push @events, $event;
            }
        }
    );

    if (@events) {
        undef $watcher;

        print "Detected changes in:\n\n";
        print "$_->{path}\n" for @events;
        print "\n";

        my $command = $opts->{'--hook'};

        print "Running hook:\n\n";
        print $command, "\n";
        open my $fh, "$command |" or die "Can't fork\n";
        while (<$fh>) {
            print;
        }
        close $fh;

        $watcher = build_watcher(@dirs);
    }
}

sub build_watcher {
    Filesys::Notify::Simple->new([@_]);
}

# From Plack
sub is_valid {
    my ($path) = @_;

    # vim temporary file is  4913 to 5036
    # http://www.mail-archive.com/vim_dev@googlegroups.com/msg07518.html
    if ($path =~ m{(\d+)$} && $1 >= 4913 && $1 <= 5036) {
        return 0;
    }

    $path !~
      m!\.(?:git|svn)[/\\]|\.(?:bak|swp|swpx|swx)$|~$|_flymake\.p[lm]$|\.#!;
}

__END__

=head1 SYNOPSIS

  noti [--hook=<command>] [<dirs>...]
  noti -h | --help

  --hook=<command>   Command to run when files change
  -h --help          Show this screen.
  --version          Show version.
